  var rCmds = newMenu("ROI Menu Tool", 
      newArray("Set Default Group...", "Set Default Stroke Width...", "-", 
      "Set Group of Selected ROIs", "Select Group", "-", "Properties...") );

  macro "ROI Menu Tool - C037T0d15RT8c12oTfc12i" {
      cmd = getArgument();
      if (cmd=="Set Default Group...")
          setDefaultRoiGroup();
      else if (cmd=="Set Default Stroke Width...")
          setDefaultRoiStrokeWidth();
      else if (cmd=="Set Group of Selected ROIs")
          setGroup();       
       else if (cmd=="Select Group")
          selectGroup();
      else if (cmd=="Properties...")
          properties();
   }

  function properties() {
     if (selectionType==-1) {
        showMessage("Selection required");
        exit;
     }
     run("Properties... ");
  }

  function setDefaultRoiGroup() {
     group = Roi.getDefaultGroup;
     Dialog.create("Set Default Group");
     Dialog.addNumber("Default group", group);
     Dialog.show;
     group = Dialog.getNumber();
     Roi.setDefaultGroup(group);
  }

  function setDefaultRoiStrokeWidth() {
     width = Roi.getDefaultStrokeWidth;
     Dialog.create("Set Default Stroke Width");
     Dialog.addNumber("Default stroke width", width);
     Dialog.show;
     width = Dialog.getNumber();
     Roi.setDefaultStrokeWidth(width);
  }

  function setGroup() {
     Dialog.create("Set Group");
     Dialog.addString("Group", "1");
     Dialog.show;
     group = Dialog.getString();
     call("ij.plugin.frame.RoiManager.setGroup", group);
  }

  function selectGroup(){
     Dialog.create("Select group");
     Dialog.addString("Group", "0");
     Dialog.show;
     group = Dialog.getString();
     call("ij.plugin.frame.RoiManager.selectGroup", group);
  }
