// Strips comments from text file containing three 512x512 images

  text = File.openAsString("");
  lines = split(text,"\n");
  text = "";  // reclaims ~30MB
  print(lines.length, "lines");
  temp = getDirectory("temp")+"temp.txt";
  f = File.open(temp);
  print("saving...");
  count = 0;
  for (i=1; i<lines.length; i++) {
      if (lengthOf(lines[i])>100) {
          print(f, lines[i]);
          count++;
      }
  }
  print(count, "lines saved");
  print( "opening as image...");
  run("Text Image... ", "open="+temp);
  File.delete(temp);
  id = getImageID;
  makeRectangle(0, 0, 512, 512);
  run("Duplicate...", "title=1");
  selectImage(id);
  makeRectangle(0, 512, 512, 512);
  run("Duplicate...", "title=2");
  selectImage(id);
  makeRectangle(0, 1024, 512, 512);
  run("Duplicate...", "title=3");
  selectImage(id);
  close;



