  parent = "C:\\images\\";
  parent = "/Users/Shared/Downloads/dan/";
  startingImage = 1;
  increment = 1;

  requires("1.39l");
  Dialog.create("Open Virtual Stack");
  Dialog.addString("Parent Dir.:", parent, 25);
  Dialog.addNumber("Starting Image:", startingImage);
  Dialog.addNumber("Increment:", increment);
  Dialog.show();
  parent = Dialog.getString();
  startingImage = Dialog.getNumber();
  increment = Dialog.getNumber();

  if (!File.exists(parent))
      exit("The directory <"+parent+"> was not found");
  list = getFileList(parent);
  dir = newArray(100);
  n = 0;
  for (i=0; i<list.length; i++) {
      path = parent+list[i];
      if (File.exists(path) && File.isDirectory(path)) {
          dir[n] = path;
          n++;   
      }
  }
  if (n==0)
      exit("<"+parent+"> does not contain any directories");

  path = getDirectory("temp")+"list.txt";
  f = File.open(path);
  for (i=0; i<n; i++) {
      list = getFileList(dir[i]);
      for (j=startingImage-1; j<list.length; j+=increment)
          print(f, dir[i]+list[j]);
  }
  File.close(f);
  run("Stack From List...", "open="+path+" use");

