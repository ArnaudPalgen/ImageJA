// A color picker with all-macro sliders
// See Macro_Sliders_Demo.txt for a simpler sliders demo.
// jerome.mutterer(at)ibmp.fr

var w=10;
var h=20;
var ch =newArray(50,80,160);
var colors =newArray("red","green","blue");
var name = "RGB Color Picker";
var active = 0;

macro "Slider Tool - Cf00L03f3F7233C0c0L07f7F3633C00fL0bfbFaa33" {
	getCursorLoc(x,y,z,flags);
	if (getTitle!=name) {
		v=getPixel(x,y);
		if (active==0) setForegroundColor((v>>16)&0xff,(v>>8)&0xff,v&0xff);
		else setBackgroundColor((v>>16)&0xff,(v>>8)&0xff,v&0xff);
		ch[0]=(v>>16)&0xff;
		ch[1]=(v>>8)&0xff;
		ch[2]=v&0xff;
		selectWindow(name);
		for (i=0;i<ch.length;i++) drawSlider(i);
		updateColor();
		exit();
	} 
	c = floor(y/h);
	while (flags!=0) {
		getCursorLoc(x,y,z,flags);
		if ((x>=0)&&(x<=255)&&(c<ch.length)) {
		ch[c]=x;
		drawSlider(c);
		updateColor();
		} else if (c>=ch.length) {
		v=getPixel(x,y);
		if (x<getWidth/2) { 
			active =0;
			setForegroundColor((v>>16)&0xff,(v>>8)&0xff,v&0xff);
		} else {
			active =1;
			setBackgroundColor((v>>16)&0xff,(v>>8)&0xff,v&0xff);
		}
		ch[0]=(v>>16)&0xff;
		ch[1]=(v>>8)&0xff;
		ch[2]=v&0xff;
		for (i=0;i<ch.length;i++) drawSlider(i);
		updateColor();
		}
	}
}

macro "Slider Tool Selected" {
	setBatchMode(true);
	call("ij.gui.ImageWindow.centerNextImage");
	newImage(name,"RGB Color",265,h*ch.length,1);
	run("Canvas Size...", "width=265 height="+getHeight+30+" position=Top-Center");
	for (i=0;i<ch.length;i++) drawSlider(i);
	updateColor();
	setBatchMode(false);
}

macro "macro "Slider Tool Options" {
	showMessage (name,"adjust the RBG sliders,\nleft color is foreground,\nright color is background");
}

macro "Help Action Tool - C000T4f15?" {
	showMessage (name,"adjust the RBG sliders,\nleft color is foreground,\nright color is background");
}

function drawSlider(i) {
	setColor(255,255,255); fillRect(0,i*h, getWidth,h);
	setColor(220,220,220); drawLine(0,i*h+h/2,getWidth,i*h+h/2);
	setColor(64,64,64); drawRect(ch[i],i*h,w,h);
	setColor(colors[i]); fillRect(ch[i]+1,i*h+1,w-2,h-2);
	offset = 15; if (ch[i]>getWidth/2) offset = -20;
	setColor(0); 	setFont("SansSerif",9);
	drawString (ch[i],ch[i]+offset, i*h+15); 
}


function updateColor() {
setColor(255,255,255); drawRect(0, getHeight-30, getWidth, 30);
if (active==0)  { 
	setColor(0,0,0);drawRect(0, getHeight-30, getWidth/2, 30);
	setColor(ch[0],ch[1],ch[2]);fillRect(1, getHeight-30+1, getWidth/2-2, 30-1);
} else {
	setColor(0,0,0);drawRect(getWidth/2-1, getHeight-30, getWidth-getWidth/2+2, 30);
	setColor(ch[0],ch[1],ch[2]);fillRect(getWidth/2, getHeight-30+1, getWidth-getWidth/2, 30-1);
}
}
